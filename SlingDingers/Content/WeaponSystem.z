
enum WeaponType
{
    Type_Projectile,
    Type_Dunk,
    Type_Laser,
    Type_Arc
}

enum WeaponName
{
    Pistol,
    MachineGun,
    Dunker,
    Laser,
    Bomb
}

class WeaponSystem : ZilchComponent
{
    [Property]
    var DefaultWeapon : Archetype;
    
    [Property]
    var CurrentWepaon : Archetype;
    
    var RoF : Real;
    var MaxAmmo : Integer;
    var Ammo : Integer;
    var Delay : Real = 10;
    var GunType : WeaponName;
    
    sends AmmoUpdate : AmmoEvent;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CollisionStarted, this.OnCollisionStarted);
        this.Equip();
    }

    function OnCollisionStarted(event : CollisionEvent)
    {
        if(event.OtherObject.PowerUp != null)
        {
            this.CurrentWepaon = event.OtherObject.PowerUp.Weapon;
            this.Equip();
            
            event.OtherObject.Destroy();
        }
    }
    
    function Equip()
    {
        var bulletObj = this.Space.CreateAtPosition(this.CurrentWepaon, Real3(0,-200,0));
        this.RoF = bulletObj.BulletScript.RoF;
        this.Ammo = bulletObj.BulletScript.Ammo;
        this.GunType = bulletObj.BulletScript.GunType;
        bulletObj.Destroy();
        
        //TODO: fix player sent when players exist
        //TODO: send to HUD instead of space maybe
        this.Space.DispatchEvent(Events.AmmoUpdate, new AmmoEvent(1, this.GunType, this.Ammo, this.MaxAmmo));
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        this.Delay += event.Dt;
        
        if(Zero.Keyboard.KeyIsDown(Keys.Space))
        {
            this.Fire(Real3(1,1,1));
        }
    }
    
    function Fire(vec : Real3)
    {
        if(this.Ammo <= 0) 
        {
            this.CurrentWepaon = this.DefaultWeapon;
            this.Equip();
        }
        
        if(this.Delay <= this.RoF) return;
        
        this.LevelSettings.CameraViewport.Camera.ScreenShake.ShakeLeoApproved(this.Owner.Transform.Translation);
        
        --this.Ammo;
        
        this.Delay = 0;
        var bulletObj = this.Space.CreateAtPosition(this.CurrentWepaon, Real3((this.Owner.Transform.Translation + (vec)).XY, 0));
        
        bulletObj.BulletScript.Propell(vec, bulletObj.BulletScript.Type);
        
        this.Owner.CharacterController.Vibrate(0.25,0.25);
        
        if(bulletObj.BulletScript.Type == WeaponType.Type_Dunk) bulletObj.RigidBody.Velocity += Real3(0,-50,0);
    }
}
